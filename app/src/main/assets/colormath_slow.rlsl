const vec4  kRGBToYPrime = vec4 (0.299, 0.587, 0.114, 0.0);
const vec4  kRGBToI     = vec4 (0.596, -0.275, -0.321, 0.0);
const vec4  kRGBToQ     = vec4 (0.212, -0.523, 0.311, 0.0);

const vec4  kYIQToR   = vec4 (1.0, 0.956, 0.621, 0.0);
const vec4  kYIQToG   = vec4 (1.0, -0.272, -0.647, 0.0);
const vec4  kYIQToB   = vec4 (1.0, -1.107, 1.704, 0.0);

vec4 colormath_rgb2yiq(vec4 thecolor){
   return vec4(dot (thecolor, kRGBToYPrime),
               dot (thecolor, kRGBToI),
               dot (thecolor, kRGBToQ),
               thecolor.w);
}

vec4 colormath_yiq2ycbcr(vec4 theyiq){
   return vec4(theyiq.x,
               atan(theyiq.z, theyiq.y),
               sqrt(theyiq.y * theyiq.y + theyiq.z * theyiq.z),
               theyiq.w);
}

vec4 colormath_rgb2ycbcr(vec4 thergb){
   return colormath_yiq2ycbcr(colormath_rgb2yiq(thergb));
}

vec4 colormath_ycbcr2yiq(vec4 theycbcr){
   return vec4(theycbcr.x,
               theycbcr.z * cos(theycbcr.y),
               theycbcr.z * sin(theycbcr.y),
               theycbcr.w);
}

vec4 colormath_yiq2rgb(vec4 theyiq){
   
   return vec4(dot(theyiq, kYIQToR),
               dot(theyiq, kYIQToG),
               dot(theyiq, kYIQToB),
               theyiq.w);
   
}

vec4 colormath_ycbcr2rgb(vec4 theycbcr){
   return colormath_yiq2rgb(colormath_ycbcr2yiq(theycbcr));
}

// and the actual library..

vec4 colormath_huerot(vec4 thecolor, float thehuerot){
   vec4 ycbcr = colormath_rgb2ycbcr(thecolor);
   ycbcr.y += thehuerot;
   return colormath_ycbcr2rgb(ycbcr);
}
